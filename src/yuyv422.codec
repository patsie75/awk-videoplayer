#!/usr/bin/gawk -f

BEGIN {
  for (i=0; i<256; i++)
    ORD[sprintf("%c",i)] = i;
  RS = ".{" width * 2 "}"
}

function min(a, b) { return (a < b) ? a : b }
function max(a, b) { return (a > b) ? a : b }
function clip(a, b, c) { return min(max(a,b), c) }

function yuv2rgb(y, u, v,    c, d, e, r, g, b) {
  c = y - 16
  d = u - 128
  e = v - 128

  r = clip( 1.164383 * c                   + 1.596027 * e , 0, 255 )
  g = clip( 1.164383 * c - (0.391762 * d) - (0.812968 * e), 0, 255 )
  b = clip( 1.164383 * c +  2.017232 * d                  , 0, 255 )

  return sprintf("%d;%d;%d", r, g, b)
}

{
  byte = 1
  line = ""
  split(RT, data, "")

  for (x=0; x<width; x+=2)
  {
    y1 = ORD[data[byte]]
    u  = ORD[data[byte+1]]
    y2 = ORD[data[byte+2]]
    v  = ORD[data[byte+3]]
    
    line = line " " yuv2rgb(y1, u, v) " " yuv2rgb(y2, u, v)

    byte += 4
  }

  printf("%s\n", line)
  fflush()
}
