#!/usr/bin/gawk -f
@include "src/ord.gawk"
@include "src/yuv.gawk"

BEGIN {
  RS = ".{" width * 2 "}"
}

{
  # receive and split data
  split(RT, data, "")

  byte = 1
  line = ""

  # call pixelformat decoding function
  for (x=0; x<width; x+=2)
  {
    y1 = ORD[data[byte]]
     u = ORD[data[byte+1]]
    y2 = ORD[data[byte+2]]
     v = ORD[data[byte+3]]

    line = line yuv2rgb(y1, u, v) " " yuv2rgb(y2, u, v) " "
    byte += 4
  }

  # return result to main thread
  printf("%s\n", line)
  fflush()
}

